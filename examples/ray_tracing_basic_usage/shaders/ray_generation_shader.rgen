#version 460
#extension GL_EXT_ray_tracing : enable

layout(push_constant) uniform PushConstants {
	mat4 viewMatrix;
    vec4 lightDir;
} pushConstants;

layout(set = 0, binding = 0, rgba8) uniform image2D img;
layout(set = 1, binding = 0) uniform accelerationStructureEXT topLevelAS;

layout(location = 0) rayPayloadEXT vec3 hitValue; // payload to traceRayEXT

void main() 
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);

    vec2 d = inUV * 2.0 - 1.0;
    float aspectRatio = float(gl_LaunchSizeEXT.x) / float(gl_LaunchSizeEXT.y);
	
    vec3 origin = vec3(0, 0, 0.0);
    vec3 direction = normalize(vec3(d.x * aspectRatio, d.y, sqrt(3))); // 1 => sqrt(3) is the scaling factor from a fov of 90 to 60
	vec4 p1 = vec4(origin, 1.0);
	vec4 p2 = vec4(origin + direction, 1.0);
	mat4 cameraTransform = inverse(pushConstants.viewMatrix);
	vec4 vp1 = cameraTransform * p1;
	vec4 vp2 = cameraTransform * p2;
	origin = vec3(vp1);
	direction = vec3(normalize(vp2 - vp1));
	
    imageStore(img, ivec2(gl_LaunchIDEXT.xy), vec4(0.0, 0.0, 0.0, 0.0));

    uint rayFlags = gl_RayFlagsOpaqueEXT;
    uint cullMask = 0xff;
    float tmin = 0.001;
    float tmax = 100.0;
    traceRayEXT(topLevelAS, rayFlags, cullMask, 0 /*sbtRecordOffset*/, 0 /*sbtRecordStride*/, 0 /*missIndex*/, origin, tmin, direction, tmax, 0 /*payload*/);

    imageStore(img, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 0.0));
}
